From Complexity.L.Datatypes Require Import LNat LSum LTerm LOptions.
From Complexity.L Require Import Functions.Decoding ComputableTime.
(* TODO: port LTDlist *)

Class linTimeDecodable `(X:Type) `{decodable X}: Type :=
  {
    c__linDec : nat;
    comp_enc_lin : computableTime' (decode X) (fun x _ => (size x *c__linDec + c__linDec,tt));
  }.

Arguments linTimeDecodable : clear implicits.
Arguments linTimeDecodable _ {_ _}.

Arguments c__linDec : clear implicits.
Arguments c__linDec _ {_ _ _}.

Global Existing Instance comp_enc_lin.

#[global]
Instance linDec_bool : linTimeDecodable bool.
Proof.
  evar (c : nat). exists c. unfold decode, decode_bool. extract. 
  solverec. [c]: exact 5. all: subst c; lia.
Qed. 

#[global]
Instance linDec_nat : linTimeDecodable nat.
Proof.
  evar (c:nat). exists c.
  unfold decode,decode_nat;cbn. extract.
  recRel_prettify2;cbn[size];ring_simplify.
  [c]:exact 9.
  all:unfold c;try lia.
Qed.


#[global]
Instance linDec_unit : linTimeDecodable unit.
Proof. 
  evar (c : nat). exists c. 
  unfold decode, decode_unit. cbn. extract. 
  solverec. [c]: exact 5. all: unfold c; lia. 
Qed. 

#[global]
Instance linDec_term : linTimeDecodable term.
Proof.
  evar (c:nat). exists c.
  unfold decode,decode_term;cbn. extract.
  recRel_prettify2;cbn[size];ring_simplify.
  [c]:exact (max (c__linDec nat) 10).
  all:unfold c;try nia.
Qed.

#[global]
Instance linDec_prod X Y `{_ : linTimeDecodable X} `{_:linTimeDecodable Y} : linTimeDecodable (X * Y). 
Proof. 
  evar (c : nat). exists c. 
  unfold decode, decode_prod, prod_decode; cbn. 
  extract. recRel_prettify2; cbn [size]; ring_simplify. 
  [c]: exact (max (max (c__linDec X) (c__linDec Y)) 14). all: unfold c; try nia. 
Qed. 

#[global]
Instance linDec_sum X Y `{_ : linTimeDecodable X} `{_:linTimeDecodable Y} : linTimeDecodable (X + Y). 
Proof. 
  evar (c : nat). exists c. 
  unfold decode, decode_sum, sum_decode; cbn. 
  extract. recRel_prettify2; cbn [size]; ring_simplify. 
  [c]: exact (max (max (c__linDec X) (c__linDec Y)) 14). all: unfold c; try nia. 
Qed. 

